     AFFD 60      		RTS
                  	;
                  	;
                  	;
                  	;
                  	;
     AFFE         	KEYSERV
                  	;		KEYBOARD SERVICE ROUTINE
     AFFE A5 CA   		LDA	THEKEY		; ANY KEY
     B000 F0 3E   		BEQ	KESR3		; NO
     B002 A2 14   		LDX	#$14		; LAST KEY
     B004 85 6A   		STA	TEMP
     B006 A9 00   		LDA	#$00
     B008 85 66   		STA	TIMOUT		; RESET ATRACT TIMEOUT
     B00A 85 CA   		STA	THEKEY		; TURN OFF KEY
     B00C A9 11   		LDA	#$11
     B00E 8D 1B D0		STA	PRIOR		; RESET PRIORITY , FROM STARBASE
     B011         	KEYSR1
     B011 BD BE BA		LDA	CODCON,X		; KEY CODES
     B014 C5 6A   		CMP	TEMP
     B016 F0 08   		BEQ	KEYSR2
     B018 CA      		DEX
     B019 10 F6   		BPL	KEYSR1		; NEXT KEY
                  	;			NO KEY
     B01B A0 10   		LDY	#SENWHT-SENTAB		; WHAT
     B01D 4C 23 B2		JMP	LDMESS
     B020         	KEYSR2			; KEY FOUND
     B020 E0 0A   		CPX	#$0A		; IMPULSE ENGINE ?
     B022 B0 1D   		BCS	KEYSR4		; NO
     B024 A5 C0   		LDA	HFLAG		; HWARP ?
     B026 F0 03   		BEQ	KEYS20		; NO
     B028 4C 80 A9		JMP	HABORT
     B02B         	KEYS20
     B02B 2C 93 09		BIT	DAMAGE+1		; ENGINES
     B02E 50 06   		BVC	KEYS23
     B030 E0 06   		CPX	#$06
     B032 90 02   		BCC	KEYS23
     B034 A2 05   		LDX	#$05
     B036         	KEYS23
     B036 BD D3 BA		LDA	WENTAB,X
     B039 85 80   		STA	WPENER		; IMPULSE ENGINE ENERGY
     B03B BD B4 BA		LDA	WARPTB,X		; SPEED
     B03E 85 71   		STA	WARP		; SPEED DESIRED
     B040         	KEYSR3
     B040 60      		RTS
     B041         	KEYSR4
     B041 E0 0E   		CPX	#$0E		; DISPLAY TYPE KEY ?
     B043 B0 1B   		BCS	KEYSR5		; NO
                  	;
     B045         	KEYS15		; ENTRY POINT TO INIT DISPLAY,  ***********************
                  	;		X MUST BE DEFINED TO THE KEY CODE IN CODCON
     B045 BD 18 8E		LDA	DISTYP-10,X
     B048 85 D0   		STA	DISFLG
     B04A BC 82 BA		LDY	DISDIS-10,X
     B04D A2 02   		LDX	#DISPL1-DISPLY
     B04F A9 08   		LDA	#$08
     B051 20 F1 AD		JSR	LDISP
                  	;
     B054 A2 10   		LDX	#STLAST
     B056         	KEYSR6
